plugins {
    id "java-library"
    id "maven-publish"
}

base {
    archivesName = "chunksfadein-${project.name}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
}

repositories {
    mavenCentral()

    exclusiveContent {
        forRepository {
            maven {
                name = "Sponge"
                url = "https://repo.spongepowered.org/repository/maven-public"
            }
        }
        filter { includeGroupAndSubgroups("org.spongepowered") }
    }

    exclusiveContent {
        forRepositories(
            maven {
                name = "ParchmentMC"
                url = "https://maven.parchmentmc.org/"
            },
            maven {
                name = "NeoForge"
                url = "https://maven.neoforged.net/releases"
            }
        )
        filter { includeGroup("org.parchmentmc.data") }
    }

    maven { url = "https://maven.blamejared.com" }
    maven { url = "https://maven.terraformersmc.com/releases/" }
    maven { url = "https://api.modrinth.com/maven" }
}

processResources {
    var expandProps = [
            "version"                      : version,
            "group"                        : project.group,
            "minecraft_version"            : minecraft_version,
            "fabric_version"               : fabric_version,
            "fabric_loader_version"        : fabric_loader_version,
            "neoforge_version"             : neoforge_version,
            "java_version"                 : java_version
    ]

    filesMatching(["fabric.mod.json", "META-INF/neoforge.mods.toml"]) {
        expand expandProps
    }

    inputs.properties(expandProps)
}
